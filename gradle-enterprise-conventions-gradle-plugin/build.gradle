plugins {
	id "checkstyle"
	id "io.spring.javaformat" version "$javaFormatVersion"
	id "java-gradle-plugin"
	id "maven-publish"
}

description = "Gradle Enterprise Conventions Gradle plugin"

repositories {
	mavenCentral()
	gradlePluginPortal()
}

configurations {
	compatibilityTestImplementation
}

dependencies {
	implementation(project(":gradle-enterprise-conventions-core"))
	compatibilityTestImplementation("com.gradle:gradle-enterprise-gradle-plugin:3.4")
	compileOnly("com.gradle:gradle-enterprise-gradle-plugin:${gradleEnterprisePluginVersion}")
	testImplementation("com.gradle:gradle-enterprise-gradle-plugin:${gradleEnterprisePluginVersion}")
}

gradlePlugin {
	plugins {
		gradleEnterprisePlugin {
			displayName = project.description
			description = project.description
			id = 'io.spring.ge.conventions'
			implementationClass = 'io.spring.ge.conventions.gradle.GradleEnterpriseConventionsPlugin'
		}
	}
}

task compatibilityTest(type: Test) {
	classpath = configurations.compatibilityTestImplementation + test.classpath
	filter {
		includeTestsMatching "*IntegrationTests"
	}
}

check {
	dependsOn compatibilityTest
}
